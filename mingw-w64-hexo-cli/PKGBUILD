# Maintainer: gouuyh <gouuyh@outlook.com>
# Contributor: Tommy Chen <tommy351@gmail.com>

# Disable this for bootstrapping
_enable_gir=yes

_realname=hexo-cli
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.3.2
pkgrel=1
pkgdesc="Command line interface for Hexo"
url="https://www.npmjs.com/package/$pkgname"
license=('MIT')
msys2_references=(
  "aur: hexo-cli"
  "cpe: cpe:/a:hexojs:hexo"
)
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
depends=("${MINGW_PACKAGE_PREFIX}-nodejs")
optdepends=("${MINGW_PACKAGE_PREFIX}-git: initialize and deploy websites"
            "${MINGW_PACKAGE_PREFIX}-yarn: npm alternative"
            "${MINGW_PACKAGE_PREFIX}-rsync: deploy websites")
conflicts=("${MINGW_PACKAGE_PREFIX}-nodejs-hexo" "${MINGW_PACKAGE_PREFIX}-nodejs-hexo-cli")
replaces=("${MINGW_PACKAGE_PREFIX}-nodejs-hexo-cli")
install=newbie-hint.install
source=("https://registry.npmjs.org/hexo-cli/-/hexo-cli-$pkgver.tgz")
noextract=("hexo-cli-$pkgver.tgz")
sha256sums=("c52c27f5847579e6e57086681951c4ac9922445316c5a6ad03a8415b0846faf6")

package() {
    npm install --global "$srcdir/hexo-cli-$pkgver.tgz" \
        --prefix "${pkgdir}${MINGW_PREFIX}" \
        --cache "$srcdir/npm-cache"

    license_file='package/LICENSE'
    tar --extract --file="$srcdir/hexo-cli-$pkgver.tgz" "$license_file"
    install -Dm644 "$license_file" "${pkgdir}${MINGW_PREFIX}/share/licenses/hexo-cli/LICENSE"
    
    # Non-deterministic race in npm gives 777 permissions to random directories.
    # See https://github.com/npm/cli/issues/1103 for details.
    find "${pkgdir}${MINGW_PREFIX}" -type d -exec chmod 755 {} +

    # npm gives ownership of ALL FILES to build user
    # https://bugs.archlinux.org/task/63396
    # chown --recursive root:root "${pkgdir}"
}